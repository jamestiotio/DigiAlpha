// RAGUL BALAJI 2020
// SUTD 50.002 Computation Structures 2D WEEK!

.include "../nominal.jsim"
.include "../stdcell.jsim" 
//.include "../timings/2dcheckoff_100ns.jsim" // 0 points
//.include "../timings/2dcheckoff_6ns.jsim" // 1 points
.include "../timings/2dcheckoff_4ns.jsim" // 3 points
//.include "../timings/2dcheckoff_3ns.jsim" // 6 points

.subckt bitbond a b
.connect a b
.ends

//NAND2 only and3 TPD 0.07ns down from 0.15ns
.subckt f4st_and3 a b c z 
Xnabc a b c iz nand3
Xzout iz z inverter
.ends

//NAND2 only or2 TPD 0.06ns down from 0.15ns
.subckt f4st_or2 a b z
Xna a a aa nand2
Xnb b b bb nand2
Xnz aa bb z nand2
.ends

//NAND2 only or4 TPD 0.12ns down from 0.29ns
.subckt f4st_or4 a b c d z
Xna a b ab f4st_or2
Xnb c d cd f4st_or2
Xnz ab cd z f4st_or2
.ends

//NAND2 only xor2 TPD 0.09ns down from 0.14ns
.subckt f4st_xor2 a b z
Xnand1  a   b   i1   nand2
Xnand2A a   i1  i2a  nand2
Xnand2B b   i1  i2b  nand2
Xnand3  i2a i2b z   nand2
.ends

// NAND2 only FULL ADDER
// https://www.geeksforgeeks.org/wp-content/uploads/nand-2.png
.subckt FA a b ci s co
Xnand1  a   b   i1   nand2
Xnand2A a   i1  i2a  nand2
Xnand2B b   i1  i2b  nand2
Xnand3  i2a i2b i3   nand2

Xnand4  ci  i3  i4   nand2
Xnand5A i3  i4  i5a  nand2
Xnand5B ci  i4  i5b  nand2
Xnand6A i5a i5b s    nand2  // SUM OUT!

Xnand6B i1  i4  co   nand2  // CARRY OUT!
.ends

// ADD / SUB
.subckt adder32 op0 A[31:0] B[31:0] S[31:0] z v n
// XOR invert
XxorB B[31:0] op0#32 xb[31:0] f4st_xor2

Xlow  LOW  constant0
Xhigh HIGH constant1

// CARRY SELECT ARRAY https://www.youtube.com/watch?v=S2c7pAFdP84
// FA 0-4
XfabitA A[4:0] xb[4:0] COA[3:0] op0 S[4:0] COA[4:0] FA

// FA 5-11
XfabitBL A[11:5] xb[11:5] COBL[5:0]  LOW SBL[11:5] COBL[6:0] FA
XfabitBH A[11:5] xb[11:5] COBH[5:0] HIGH SBH[11:5] COBH[6:0] FA
XBmux  COA[4]#7 SBL[11:5] SBH[11:5] S[11:5] mux2
XBmuxo COA[4] COBL[6] COBH[6] COB[6] mux2

// FA 12-20
XfabitCL A[20:12] xb[20:12] COCL[7:0]  LOW SCL[20:12] COCL[8:0] FA
XfabitCH A[20:12] xb[20:12] COCH[7:0] HIGH SCH[20:12] COCH[8:0] FA
XCmux  COB[6]#9 SCL[20:12] SCH[20:12] S[20:12] mux2
XCmuxo COB[6] COCL[8] COCH[8] COC[8] mux2

// FA 21-31
XfabitDL A[31:21] xb[31:21] CODL[9:0]  LOW SDL[31:21] CODL[10:0] FA
XfabitDH A[31:21] xb[31:21] CODH[9:0] HIGH SDH[31:21] CODH[10:0] FA
XDmux  COC[8]#11 SDL[31:21] SDH[31:21] S[31:21] mux2
XDmuxo COC[8] CODL[10] CODH[10] COD[10] mux2

// Full adders # RIPPLE
//Xfabits A[31:0] xb[31:0] CO[30:0] op0 S[31:0] CO[31:0] FA

// Zero Bit - "Big NOR"
Xors1 S[7:0] S[15:8] S[23:16] S[31:24] toors2[7:0] f4st_or4
Xors2 toors2[1:0] toors2[3:2] toors2[5:4] toors2[7:6] toors3[1:0] f4st_or4
Xors3 toors3[0] toors3[1] z nor2 

// Overflow Bit
XnotA31 A[31] notA31 inverter
XnotXB31 xb[31] notXB31 inverter
XnotS31 S[31] notS31 inverter
Xflow1a A[31] xb[31] notS31 flow1a f4st_and3
Xflow1b notA31 notXB31 S[31] flow1b f4st_and3
Xflowout flow1a flow1b v f4st_or2

// Negative Bit
Xnegbit S[31] n bitbond
.ends