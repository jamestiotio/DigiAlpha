.include "../nominal.jsim"
.include "../stdcell.jsim" 
//.include "../timings/2dcheckoff_100ns.jsim" // 0 points
//.include "../timings/2dcheckoff_6ns.jsim" // 1 points
//.include "../timings/2dcheckoff_4ns.jsim" // 3 points
.include "../timings/2dcheckoff_3ns.jsim" // 6 points
//.include "../timings/2dcheckoff_2p8ns.jsim" // BONUS PASSES

// 32 BIT HAN CARLSON PARALLEL PREFIX ADDER
// Making reference to http://cc.sjtu.edu.cn/upload/20150615174224327.pdf
.subckt adder32 op0 A[31:0] B[31:0] S[31:0] z v n

// Implement hex inversion instead of regular XOR2 for high fidelity pipe-lining, at the cost of more area usage
// Double inverters serve to amplify the digital signal power (high fan-out capabilities)
XinvB B[31:0] ib[31:0] inverter
Xinvop0 op0 opmid inverter_2
Xinvopmid1 opmid op1 inverter_4
Xinv2opmid2 opmid op2 inverter_2
Xinv2opmid3 opmid op3 inverter_2
Xinv2opmid4 opmid op4 inverter
XselBa op1#8 B[7:0] ib[7:0] xb[7:0] mux2
XselBb op2#8 B[15:8] ib[15:8] xb[15:8] mux2
XselBc op3#8 B[23:16] ib[23:16] xb[23:16] mux2
XselBd op4#8 B[31:24] ib[31:24] xb[31:24] mux2

// PropGen Blocks - 32 of them
Xpg A[31:1] xb[31:1] L0P[31:1] L0G[31:1] GenPG
Xpng0 A[0] xb[0] L0P[0] g0pre GenPG
Xprep L0P[0] g0pre op0 L0G[0] greybox

// Layer 1 - L0P L0G >> L1P L1G
XL1pass0 L0G[30:0:2] L1G[30:0:2] bitbond
XL1pass1 L0P[30:0:2] L1P[30:0:2] bitbond
XL1grey L0P[1] L0G[1] L0G[0] L1G[1] greybox
XL1blac L0P[31:3:2] L0G[31:3:2] L0P[30:2:2] L0G[30:2:2] L1P[31:3:2] L1G[31:3:2] blackbox

// Layer 2 - L1P L1G >> L2P L2G
XL2pass0 L1G[2] L2G[2] bitbond
XL2pass1 L1P[2:0:2] L2P[2:0:2] bitbond
XL2buf L1G[1:0] L2G[1:0] buffer
XL2pass2 L1G[30:6:4] L2G[30:6:4] bitbond
XL2pass3 L1G[29:5:4] L2G[29:5:4] bitbond
XL2pass4 L1G[28:4:4] L2G[28:4:4] bitbond
XL2pass5 L1P[30:6:4] L2P[30:6:4] bitbond
XL2pass6 L1P[29:5:4] L2P[29:5:4] bitbond
XL2pass7 L1P[28:4:4] L2P[28:4:4] bitbond
XL2grey L1P[3] L1G[3] L1G[1] L2G[3] greybox
XL2blac L1P[31:7:4] L1G[31:7:4] L1P[29:5:4] L1G[29:5:4] L2P[31:7:4] L2G[31:7:4] blackbox

// Layer 3 - L2P L2G >> L3P L3G
XL3pass0 L2P[2:0:2] L3P[2:0:2] bitbond
XL3pass1 L2G[3] L3G[3] bitbond
XL3pass2 L2G[2:0] L3G[2:0] bitbond
XL3pass3 L2G[30:6:4] L3G[30:6:4] bitbond
XL3pass4 L2G[29:5:4] L3G[29:5:4] bitbond
XL3pass5 L2G[28:4:4] L3G[28:4:4] bitbond
XL3pass6 L2P[30:6:4] L3P[30:6:4] bitbond
XL3pass7 L2P[29:5:4] L3P[29:5:4] bitbond
XL3pass8 L2P[28:4:4] L3P[28:4:4] bitbond
XL3grey L2P[7] L2G[7] L2G[3] L3G[7] greybox
XL3blac L2P[31:11:4] L2G[31:11:4] L2P[27:7:4] L2G[27:7:4] L3P[31:11:4] L3G[31:11:4] blackbox

// Layer 4 - L3P L3G >> L4P L4G
XL4pass0 L3P[2:0:2] L4P[2:0:2] bitbond
XL4pass1 L3G[6:0] L4G[6:0] bitbond
XL4pass2 L3G[7] L4G[7] bitbond
XL4pass3 L3P[6:4] L4P[6:4] bitbond
XL4pass4 L3G[30:10:4] L4G[30:10:4] bitbond
XL4pass5 L3G[29:9:4] L4G[29:9:4] bitbond
XL4pass6 L3G[28:8:4] L4G[28:8:4] bitbond
XL4pass7 L3P[30:10:4] L4P[30:10:4] bitbond
XL4pass8 L3P[29:9:4] L4P[29:9:4] bitbond
XL4pass9 L3P[28:8:4] L4P[28:8:4] bitbond
XL4grey L3P[15:11:4] L3G[15:11:4] L3G[7:3:4] L4G[15:11:4] greybox
XL4blac L3P[31:19:4] L3G[31:19:4] L3P[23:11:4] L3G[23:11:4] L4P[31:19:4] L4G[31:19:4] blackbox

// Layer 5 - L4P L4G >> L5P L5G
XL5pass0 L4G[10:0] L5G[10:0] bitbond
XL5pass1 L4G[14:12] L5G[14:12] bitbond
XL5pass2 L4G[18:16] L5G[18:16] bitbond
XL5pass3 L4G[15:11:4] L5G[15:11:4] bitbond
XL5pass4 L4P[18:16] L5P[18:16] bitbond
XL5pass5 L4P[14:12] L5P[14:12] bitbond
XL5pass6 L4P[10:8] L5P[10:8] bitbond
XL5pass7 L4P[6:4] L5P[6:4] bitbond
XL5pass8 L4P[2:0:2] L5P[2:0:2] bitbond
XL5pass9 L4G[22:20] L5G[22:20] bitbond
XL5pass10 L4P[22:20] L5P[22:20] bitbond
XL5pass11 L4G[26:24] L5G[26:24] bitbond
XL5pass12 L4P[26:24] L5P[26:24] bitbond
XL5pass13 L4G[30:28] L5G[30:28] bitbond
XL5pass14 L4P[30:28] L5P[30:28] bitbond
XL5grey L4P[31:19:4] L4G[31:19:4] L4G[15:3:4] L5G[31:19:4] greybox

// Layer 6 - L5P L5G >> L6P L6G
XL6pass0 L5G[4:0] L6G[4:0] bitbond
XL6pass1 L5P[30:2:2] L6P[30:2:2] bitbond
XL6pass2 L5G[8:6:2] L6G[8:6:2] bitbond
XL6buf0 L5G[7] L6G[7] buffer
XL6pass3 L5G[12:10] L6G[12:10] bitbond
XL6pass4 L5G[16:14] L6G[16:14] bitbond
XL6pass5 L5G[20:18:2] L6G[20:18:2] bitbond
XL6pass6 L5G[24:22:2] L6G[24:22:2] bitbond
XL6pass7 L5G[30:26:2] L6G[30:26:2] bitbond
XL6pass8 L5G[23:19:4] L6G[23:19:4] bitbond
XL6buf1 L5G[27] L6G[27] buffer
XL6pass9 L5G[31] L6G[31] bitbond
XL6grey L5P[29:5:4] L5G[29:5:4] L5G[27:3:4] L6G[29:5:4] greybox

// Layer 7 - L6G >> L7G
XL7pass0 L6G[1:0] L7G[1:0] bitbond
XL7pass1 L6G[31:3:4] L7G[31:3:4] bitbond
XL7pass2 L6G[13:5:4] L7G[13:5:4] bitbond
XL7buf0 L6G[17] L7G[17] buffer
XL7pass3 L6G[25:21:4] L7G[25:21:4] bitbond
XL7buf1 L6G[29] L7G[29] buffer
XL7grey L6P[30:2:2] L6G[30:2:2] L6G[29:1:2] L7G[30:2:2] greybox

// COMPUTE Sum
Xpsum L0P[31:0] L7G[30:0] op0 psum[31:0] nor2
Xfinal L0P[31:0] L7G[30:0] op0 psum[31:0] S[31:0] aoi21

// Overflow Bit
XnotA31 A[31] notA31 inverter
XnotXB31 xb[31] notXB31 inverter
XnotS31 S[31] notS31 inverter
Xflow1a A[31] xb[31] notS31 flow1a nand3
Xflow1b notA31 notXB31 S[31] flow1b nand3
Xflowout flow1a flow1b v nand2

// Zero Bit - "Big NOR" INVERTING LOGIC VERSION
Xors1 S[7:0] S[15:8] S[23:16] S[31:24] toors2[7:0] nor4
Xors2 toors2[7:6] toors2[5:4] toors2[3:2] toors2[1:0] toors3[1:0] nand4
Xors3 toors3[1] toors3[0] z nor2

// Negative Bit
Xnegbit S[31] n bitbond
.ends

// whitebox cell
.subckt GenPG a b p g
XgenP a b p1 nor2
XgenP2 a b p1 p aoi21
XgenG a b g f4st_and2
.ends

// gg = (pt AND gl) OR gt
// pp = pt AND pl
.subckt blackbox pt gt pl gl pp gg
Xaoi pt gl gt igg aoi21 
Xao  igg gg inverter
Xpap pt pl pp f4st_and2
.ends

// gg = (pt AND gl) OR gt
.subckt greybox pt gt gl gg
Xaoi pt gl gt igg aoi21
Xao  igg gg inverter
.ends

.subckt bitbond a b
.connect a b
.ends

// -------------------------------- FAST PARTS BELOW -------------------------------- //

//NAND2 only and2 TPD 0.05ns down from 0.12ns
.subckt f4st_and2 a b z 
Xnab a b iz nand2
Xiiz iz z inverter
.ends

//NAND3 only and3 TPD 0.07ns down from 0.15ns
.subckt f4st_and3 a b c z 
Xnabc a b c iz nand3
Xzout iz z inverter
.ends

//NAND2 only or2 TPD 0.06ns down from 0.15ns
.subckt f4st_or2 a b z
Xna a a aa nand2
Xnb b b bb nand2
Xnz aa bb z nand2
.ends