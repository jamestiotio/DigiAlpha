// RAGUL BALAJI 2020
// SUTD 50.002 Computation Structures 2D WEEK!

.include "../nominal.jsim"
.include "../stdcell.jsim" 
//.include "../timings/2dcheckoff_100ns.jsim" // 0 points
//.include "../timings/2dcheckoff_6ns.jsim" // 1 points
//.include "../timings/2dcheckoff_4ns.jsim" // 3 points
.include "../timings/2dcheckoff_3ns.jsim" // 6 points
//.include "../timings/2dcheckoff_2p8ns.jsim" // BONUS PASSES

// 32 BIT KOGGE STONE PARALLEL PREFIX ADDER
// Making reference to http://cc.sjtu.edu.cn/upload/20150615174224327.pdf
.subckt adder32 op0 A[31:0] B[31:0] S[31:0] z v n

// Implement hex inversion instead of regular XOR2 for high fidelity pipe-lining, at the cost of more area usage
// Double inverters serve to amplify the digital signal power (high fan-out capabilities)
XinvB B[31:0] ib[31:0] inverter
Xinvop0 op0 opmid inverter_2
Xinvopmid1 opmid op1 inverter_4
Xinv2opmid2 opmid op2 inverter_2
Xinv2opmid3 opmid op3 inverter_2
Xinv2opmid4 opmid op4 inverter
XselBa op1#8 B[7:0] ib[7:0] xb[7:0] mux2
XselBb op2#8 B[15:8] ib[15:8] xb[15:8] mux2
XselBc op3#8 B[23:16] ib[23:16] xb[23:16] mux2
XselBd op4#8 B[31:24] ib[31:24] xb[31:24] mux2

// PropGen Blocks - 32 of them
Xpgens A[31:0] xb[31:0] L0P[31:0] L0G[31:1] kek0 GenPG
Xgrey0 L0P[0] kek0 op0 L0G[0] greybox

// Layer 1 - L0P L0G >> L1P L1G
Xpass1 L0G[0] L1G[0] bitbond
Xgrey1 L0P[1] L0G[1] L0G[0] L1G[1] greybox
Xblac1 L0P[31:2] L0G[31:2] L0P[30:1] L0G[30:1] L1P[31:2] L1G[31:2] blackbox

// Layer 2 - L1P L1G >> L2P L2G
Xpass2 L1G[1:0] L2G[1:0] bitbond
Xgrey2 L1P[3:2] L1G[3:2] L1G[1:0] L2G[3:2] greybox
Xblac2 L1P[31:4] L1G[31:4] L1P[29:2] L1G[29:2] L2P[31:4] L2G[31:4] blackbox

// Layer 3 - L2P L2G >> L3P L3G
Xpass3 L2G[3:0] L3G[3:0] bitbond
Xgrey3 L2P[7:4] L2G[7:4] L2G[3:0] L3G[7:4] greybox
Xblac3 L2P[31:8] L2G[31:8] L2P[27:4] L2G[27:4] L3P[31:8] L3G[31:8] blackbox

// Layer 4 - L3P L3G >> L4P L4G
Xpass4 L3G[7:0] L4G[7:0] bitbond
Xgrey4 L3P[15:8] L3G[15:8] L3G[7:0] L4G[15:8] greybox
Xblac4 L3P[31:16] L3G[31:16] L3P[23:8] L3G[23:8] L4P[31:16] L4G[31:16] blackbox

// Layer 5 - L4P L4G >> L5G
Xpass5 L4G[15:0] L5G[15:0] bitbond
Xgrey5 L4P[31:16] L4G[31:16] L4G[15:0] L5G[31:16] greybox
// No need blackboxes, the terms have been computed

// COMPUTE Sum
Xfinal L0P[31:0] L5G[30:0] op0 S[31:0] xor2

// Overflow Bit
XnotA31 A[31] notA31 inverter
XnotXB31 xb[31] notXB31 inverter
XnotS31 S[31] notS31 inverter
Xflow1a A[31] xb[31] notS31 flow1a nand3
Xflow1b notA31 notXB31 S[31] flow1b nand3
Xflowout flow1a flow1b v nand2

// Zero Bit - "Big NOR" INVERTING LOGIC VERSION
Xors1 S[7:0] S[15:8] S[23:16] S[31:24] toors2[7:0] nor4
Xors2 toors2[1:0] toors2[3:2] toors2[5:4] toors2[7:6] toors3[1:0] nand4
Xors3 toors3[0] toors3[1] z nor2

// Negative Bit
Xnegbit S[31] n bitbond
.ends

// standard stuff from CLA
.subckt GenPG a b p g
XgenP a b p1 nor2
XgenP2 a b p1 p aoi21
XgenG a b g f4st_and2
.ends

// gg = (pt AND gl) OR gt
// pp = pt AND pl
.subckt blackbox pt gt pl gl pp gg 
Xaoi pt gl gt igg aoi21 
Xao  igg gg inverter
Xpap pt pl pp f4st_and2 
.ends

// gg = (pt AND gl) OR gt
.subckt greybox pt gt gl gg 
Xaoi pt gl gt igg aoi21 
Xao  igg gg inverter
.ends

.subckt bitbond a b
.connect a b
.ends

// -------------------------------- FAST PARTS BELOW -------------------------------- //

//NAND2 only and2 TPD 0.05ns down from 0.12ns
.subckt f4st_and2 a b z 
Xnab a b iz nand2
Xiiz iz z inverter
.ends

//NAND3 only and3 TPD 0.07ns down from 0.15ns
.subckt f4st_and3 a b c z 
Xnabc a b c iz nand3
Xzout iz z inverter
.ends

//NAND2 only or2 TPD 0.06ns down from 0.15ns
.subckt f4st_or2 a b z
Xna a a aa nand2
Xnb b b bb nand2
Xnz aa bb z nand2
.ends